Import('env')
lpre = 'sc'
sdir = '.'
bdir = '../../tmp/build'
ldir = '.'
libs = list()

def make_kplib(lname, lpre, ldir, sdir, bdir, env, extra_links=None):
    """
    Make kerpak libraries.
    """
    libs = []
    for ndim in 2, 3:
        VariantDir('%s/%s%dd' % (bdir, lname, ndim), sdir, duplicate=0)
        envm = env.Clone()
        envm.Prepend(NVCCFLAGS=['-DNDIM=%d'%ndim])
        if extra_links is not None:
            envm.Prepend(LIBS=extra_links)
        libs.append(envm.SharedLibrary(
            '%s/%s_%s%dd' % (ldir, lpre, lname, ndim),
            Glob('%s/%s%dd/*.cu' % (bdir, lname, ndim))))
    return libs
kplibs = [
    ('cutest', ['cublas', 'cudart', 'cuda']),
    ('cueuler', ['cudart', 'cuda']),
    ('cueulerb', ['cudart', 'cuda']),
]
for lname, extra_links in kplibs:
    libs.extend(make_kplib(lname, lpre, ldir, '%s/%s'%(sdir, lname),
        bdir, env, extra_links=extra_links))

binary = env.Program('test.so', ['test.cu', 'cutest/vecadd.cu'],
    LIBS=['cublas', 'cudart', 'cuda'])

all = Alias([libs, binary])
Default(all)
# vim: set ft=python ff=unix:
