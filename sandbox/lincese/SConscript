import os, sys

Import('env')

def make_dimlib(lname, lpre, ldir, sdir, bdir, env, extra_links=None):
    libs = []
    for ndim in 2, 3:
        VariantDir('%s/%s%dd' % (bdir, lname, ndim), sdir, duplicate=0)
        envm = env.Clone()
        envm.Prepend(CCFLAGS=['-DNDIM=%d'%ndim])
        if extra_links is not None:
            envm.Prepend(LIBS=extra_links)
        libs.append(envm.SharedLibrary(
            '%s/%s_%s%dd' % (ldir, lpre, lname, ndim),
            Glob('%s/%s%dd/*.c' % (bdir, lname, ndim))))
    return libs

lpre = 'sc'
ldir = '.'
sdir = '.'
bdir = '../../tmp/build'
libs = list()
# kerpak libraries.
for lname in ['lincese']:
    libs.extend(make_dimlib(lname, lpre, ldir, '%s/%s'%(sdir, lname),
        bdir, env, extra_links=['lapack']))
for lname in ['elaslin', 'elaslinb']:
    libs.extend(make_dimlib(lname, lpre, ldir, '%s/%s'%(sdir, lname),
        bdir, env))
all = Alias(libs)
Default(all)
# vim: set ft=python ff=unix:
